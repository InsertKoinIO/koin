apply plugin: 'org.jetbrains.kotlin.multiplatform'

archivesBaseName = 'koin-test'
description = 'Koin - simple dependency injection for Kotlin - ' + archivesBaseName
def ideaActive = System.getProperty('idea.active') == 'true'

kotlin {
    jvm()
    js() /*{
        nodejs()
        browser()
        configure([compilations.main, compilations.test]) {
            tasks.getByName(compileKotlinTaskName).kotlinOptions {
                metaInfo = true
                sourceMap = true
                moduleKind = 'umd'
            }
        }
    }*/

    if(ideaActive) {
        macosX64("native")
    }else{
        macosX64()
        iosArm32()
        iosArm64()
        iosX64()
        linuxX64()
        linuxArm32Hfp()
        linuxMips32()
        watchosArm32()
        watchosArm64()
        watchosX86()
        tvosArm64()
        tvosX64()
//    androidNativeArm32()
//    androidNativeArm64()
        mingwX64()
    }

    sourceSets {
        commonMain {
            dependencies {
                api kotlin("stdlib-common")
                api kotlin("test-annotations-common")
                api project(":koin-core")
            }
        }

        commonTest {
            dependencies {
                implementation kotlin("test-common")
                implementation kotlin("test-annotations-common")
                implementation "co.touchlab:testhelp:$testhelp_version"
                implementation "com.benasher44:uuid:$uuid_version"
            }
        }

        jvmMain {
            dependencies {
                api kotlin("stdlib-jdk8")
                api kotlin("test-junit")
            }
        }

        jvmTest {
            dependencies {
                implementation kotlin("test-junit")
                implementation "org.mockito:mockito-inline:$mockito_version"
            }
        }

        jsMain {
            dependsOn commonMain
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-js'
                implementation 'org.jetbrains.kotlin:kotlin-test-js'
            }
        }

        jsTest {
            dependsOn commonTest
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-js'
            }
        }

        nativeMain {
            dependencies {

            }
        }

        nativeTest {
            dependencies {

            }
        }

        if(!ideaActive) {
            configure([
                    targets.macosX64,
                    targets.watchosArm32,
                    targets.watchosArm64,
                    targets.watchosX86,
                    targets.tvosArm64,
                    targets.tvosX64,
                    targets.iosX64,
                    targets.iosArm32,
                    targets.iosArm64,
                    targets.linuxX64,
                    targets.linuxArm32Hfp,
                    targets.linuxMips32,
                    targets.mingwX64
            ]) {
                compilations.main.source(sourceSets.nativeMain)
                compilations.test.source(sourceSets.nativeTest)
            }
        }
    }
}

apply from: '../gradle/publish2.gradle'
