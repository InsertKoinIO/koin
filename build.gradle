apply from: "common.gradle"

buildscript {
    apply from: "common.gradle"
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:${dokka_version}"
    }
}

subprojects {
    apply plugin: 'kotlin'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'org.jetbrains.dokka'

    repositories {
        jcenter()
    }

    group = 'org.koin'
    version = "$koin_version"

    // custom tasks for creating source/javadoc jars
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar, javadocJar
    }

    dokka {
        outputFormat = 'html'
        outputDirectory = "$buildDir/javadoc"
    }

    bintray {
        Properties properties = new Properties()
        def localPropsFile = project.rootProject.file('local.properties')
        if (localPropsFile.canRead()) {
            properties.load(localPropsFile.newDataInputStream())
            user = properties.getProperty('bintray_user')
            key = properties.getProperty('bintray_apikey')

            configurations = ['archives']

            publish = true //[Default: false] Whether version should be auto published after an upload
            override = true
            pkg {
                repo = 'koin'
                name = archivesBaseName
                userOrg = 'ekito'
                licenses = ['Apache-2.0']
                vcsUrl = 'https://github.com/Ekito/koin.git'
            }
        }
    }
}

